apply plugin: 'com.android.application'

// LibJeID製品版を利用の場合はlocal.propertiesにサブスクリプションIDを設定してください
def local = new Properties()
local.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    namespace = "jp.co.osstech.jeidreader"
    compileSdkVersion 34
    defaultConfig {
        applicationId "jp.co.osstech.jeidreader"
        minSdkVersion 14
        targetSdkVersion 34
        versionCode 51
        versionName "1.0." + versionCode
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile project.rootProject.file('release.keystore')
            storePassword local.getProperty("release.storePassword")
            keyAlias local.getProperty("release.keyAlias")
            keyPassword local.getProperty("release.keyPassword")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.release
        }
    }
    buildFeatures {
        buildConfig = true
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    lintOptions {
        abortOnError = false
        disable 'SetTextI18n'
        disable 'HardcodedText'
        disable 'IconLocation'
        disable 'TextFields'
        disable 'SetJavaScriptEnabled'
        textReport = true
    }
    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                exceptionFormat 'full'
            }
        }
    }
    packaging {
        resources.excludes.add("META-INF/versions/9/OSGI-INF/MANIFEST.MF")
    }
}

repositories {
    maven {
        url = 'https://www.osstech.co.jp/download/libjeid/m2/'

        if (local.getProperty("libjeid.username") &&
            local.getProperty("libjeid.password")) {
            credentials {
                username = local.getProperty("libjeid.username")
                password = local.getProperty("libjeid.password")
            }
        }

        // for gradle 5.x
        content {
            includeGroup "jp.co.osstech"
        }
    }
}


dependencies {
    if (local.getProperty("libjeid.username")) {
        // LibJeID製品版を利用
        implementation 'jp.co.osstech:libjeid-full:20251031@aar'
    } else {
        // LibJeID無償版を利用
        implementation 'jp.co.osstech:libjeid-free:20251031@aar'
    }

    // ダウンロードしたaarファイルを利用
    //implementation project(path: ':libjeid', configuration: 'full')
    //implementation project(path: ':libjeid', configuration: 'free')

    //implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'org.bouncycastle:bcprov-lts8on:2.73.9'
    implementation 'org.bouncycastle:bcpkix-lts8on:2.73.9'
}
