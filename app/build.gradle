apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "jp.co.osstech.jeidreader"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 22
        versionName "1.0.22"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            def local = new Properties()
            local.load(project.rootProject.file('local.properties').newDataInputStream())
            storeFile project.rootProject.file('release.keystore')
            storePassword local.getProperty("release.storePassword")
            keyAlias local.getProperty("release.keyAlias")
            keyPassword local.getProperty("release.keyPassword")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    lintOptions {
        abortOnError false
        disable 'SetTextI18n'
        disable 'HardcodedText'
        disable 'IconLocation'
        disable 'TextFields'
        disable 'SetJavaScriptEnabled'
        textReport true
    }
    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                exceptionFormat 'full'
            }
        }
    }
}

repositories {
    maven {
        url 'https://www.osstech.co.jp/download/libjeid/m2/'
        def local = new Properties()
        local.load(project.rootProject.file('local.properties').newDataInputStream())
        if (local.getProperty("libjeid.username") &&
            local.getProperty("libjeid.password")) {
            credentials {
                username local.getProperty("libjeid.username")
                password local.getProperty("libjeid.password")
            }
        }
        /* for gradle 5.x
        content {
            includeGroup "jp.co.osstech"
        }
        */
    }
}

dependencies {
    // LibJeID無償版を利用
    implementation 'jp.co.osstech:libjeid-free:20200529@aar'
    // LibJeID製品版を利用
    //implementation 'jp.co.osstech:libjeid-full:20200529@aar'

    // ダウンロードしたaarファイルを利用
    //implementation project(path: ':libjeid', configuration: 'free')
    //implementation project(path: ':libjeid', configuration: 'full')

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.64'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.64'
}
